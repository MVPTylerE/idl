; >>>> begin comments
;==========================================================================================
;
; >>>> McObject Class: sdi2k_math_windmap
;
; This file contains the McObject method code for sdi2k_math_windmap objects:
;
; Mark Conde Fairbanks, October 2000.
;
; >>>> end comments
; >>>> begin declarations
;         menu_name = Wind Vector Mapper
;        class_name = sdi2k_math_windmap
;       description = SDI Analysis - Plot Wind Maps
;           purpose = SDI analysis
;       idl_version = 5.2
;  operating_system = Windows NT4.0 terminal server
;            author = Mark Conde
; >>>> end declarations

@sdi2k_ncdf.pro

;==========================================================================================
; This is the (required) "new" method for this McObject:

pro sdi2k_math_windmap_new, instance, dynamic=dyn, creator=cmd
;---First, properties specific to this object:
    common windmap_resarr, windfit
    cmd = 'instance = {sdi2k_math_windmap, '
    cmd = cmd + 'specific_cleanup: ''sdi2k_math_windmap_specific_cleanup'', '
    windmap_behavior = {windmap_behavior, prompt_for_filename: 1, $
                          menu_configurable: 0, $
                              user_editable: [1]}
    windmap_scale = {windmap_scale, auto_scale: 0, $
                                      yrange: [600.], $
                           menu_configurable: 1, $
                               user_editable: [0,1]}
    rex = [0, n_elements(resarr)-1]
    pp  = 'Map'
    oo  = 'Geographic North at Top'

    cmd = cmd + 'behavior: windmap_behavior, '
    cmd = cmd + 'scale: windmap_scale, '
    cmd = cmd + 'black_bgnd: 1, '
    cmd = cmd + 'perspective: pp, '
    cmd = cmd + 'orientation: oo, '
    cmd = cmd + 'records: rex, '
;---Now add fields common to all SDI objects. These will be grouped as sub-structures:
    sdi2k_common_fields, cmd, automation=automation, geometry=geometry
;---Next, add the required fields, whose specifications are read from the 'declarations'
;   section of the comments at the top of this file:
    whoami, dir, file
    obj_reqfields, dir+file, cmd, dynamic=dyn
;---Now, create the instance:
    status = execute(cmd)
end

;==========================================================================================
; This is the event handler for events generated by the sdi2k_math_windmap object:
pro sdi2k_math_windmap_event, event
    common windmap_tlist, tlist, tcen, datestr
    common windmap_resarr, windfit
    widget_control, event.top, get_uvalue=info
    wid_pool, 'Settings: ' + info.wtitle, sidx, /get
    if not(widget_info(sidx, /valid_id)) then return
    widget_control, sidx, get_uvalue=windmap_settings
    if widget_info(event.id, /valid_id) and windmap_settings.automation.show_on_refresh then widget_control, event.id, /show

;---Check for a timer tick:
    if tag_names(event, /structure_name) eq 'WIDGET_TIMER' then begin
       sdi2k_math_windmap_tik, info.wtitle
       if windmap_settings.automation.timer_ticking then widget_control, sidx, timer=windmap_settings.automation.timer_interval
       return
    endif

;---Get the menu name for this event:
    widget_control, event.id, get_uvalue=menu_item

    if menu_item eq 'View|Time Chooser' then begin
       mcchoice, 'Start time:', tlist, choice
       windmap_settings.records(0) = choice.index
       mcchoice, 'End time:', tlist, choice
       windmap_settings.records(1) = choice.index
       widget_control, sidx, set_uvalue=windmap_settings
    endif

    if menu_item eq 'View|Toggle Background Color' then begin
       windmap_settings.black_bgnd = 1 - windmap_settings.black_bgnd
       widget_control, sidx, set_uvalue=windmap_settings
    endif

    if menu_item eq 'View|Perspective|Sky' then begin
       windmap_settings.perspective = 'Sky'
       widget_control, sidx, set_uvalue=windmap_settings
    endif

    if menu_item eq 'View|Perspective|Map' then begin
       windmap_settings.perspective = 'Map'
       widget_control, sidx, set_uvalue=windmap_settings
    endif

    if menu_item eq 'View|Orientation|Magnetic North' then begin
       windmap_settings.orientation = 'Magnetic North at Top'
       widget_control, sidx, set_uvalue=windmap_settings
    endif

    if menu_item eq 'View|Orientation|Geographic North' then begin
       windmap_settings.orientation = 'Geographic North at Top'
       widget_control, sidx, set_uvalue=windmap_settings
    endif

    if menu_item eq 'View|Orientation|Magnetic Noon' then begin
       windmap_settings.orientation = 'Magnetic Noon at Top'
       widget_control, sidx, set_uvalue=windmap_settings
    endif

    sdi2k_math_windmap_plot, info.wtitle
    if n_elements(menu_item) eq 0 then menu_item = 'Nothing valid was selected'
end


;==========================================================================================
; This is the routine that handles timer ticks:
pro sdi2k_math_windmap_tik, wtitle, redraw=redraw, _extra=_extra
@sdi2kinc.pro
    sdi2k_math_windmap_plot, wtitle
end

;===========================================================================================
;
;   This does the actual plotting:

pro sdi2k_math_windmap_plot, wtitle
@sdi2kinc.pro
    common windmap_resarr, windfit
    common windmap_tlist,  tlist, tcen, datestr
;---Get settings information for this instance of the output xwindow and this instance of
;   the plot program itself:
    wid_pool, wtitle, widx, /get
    if not(widget_info(widx, /valid_id)) then return
    widget_control, widx, get_uvalue=info
    wid_pool, 'Settings: ' + wtitle, sidx, /get
    if not(widget_info(sidx, /valid_id)) then return
    widget_control, sidx, get_uvalue=windmap_settings


    if !d.name ne 'Z' and !d.name ne 'PS' then wset, info.wid

    sdi2k_wind_mapper, tlist, tcen, datestr, windfit, windmap_settings

;---Check if we need to make a GIF file:
    ;sdi2k_plugin_gif, info, js_time=timlimz(1)
end

;==========================================================================================
;   Cleanup jobs:
pro sdi2k_math_windmap_specific_cleanup, widid
@sdi2kinc.pro
    close, /all
;    ncdf_close, host.netcdf(0).ncid
;    host.netcdf(0).ncid = -1
end

;==========================================================================================
; This is the (required) "autorun" method for this McObject. If no autorun action is
; needed, then this routine should simply exit with no action:

pro sdi2k_math_windmap_autorun, instance
@sdi2kinc.pro
    common windmap_resarr, windfit
    common windmap_tlist, tlist, tcen, datestr
    device, get_screen_size=box
    instance.geometry.xsize = 0.9*box(0)
    instance.geometry.ysize = 0.85*min(box) + 100
    instance.automation.timer_interval = 1.
    instance.automation.timer_ticking = 0
    if instance.behavior.prompt_for_filename then begin
       spekfile = dialog_pickfile(file=skyfile, $
                                 filter='sky*.' + host.operation.header.site_code, $
                                 group=widx, title='Select a file of sky spectra: ', $
                                 path=host.operation.logging.log_directory)
    endif

    sdi2k_ncopen, spekfile, ncid, 0
    sdi2k_build_windres, ncid, windfit
    ncdf_diminq, ncid, ncdf_dimid(ncid, 'Time'),    dummy,  maxrec
    record = 0
    tlist = strarr(maxrec)
    tcen  = dblarr(maxrec)
    for rec=record,maxrec-1 do begin
        sdi2k_read_exposure, ncid, rec
        tcen(rec) = host.programs.spectra.start_time + host.programs.spectra.integration_seconds/2
        hhmm = dt_tm_mk(js2jd(0d)+1, tcen(rec), format='h$:m$')
        tlist(rec) =  hhmm
    endfor
    sdi2k_read_exposure, host.netcdf(0).ncid, 0
    ctime = host.programs.spectra.start_time + host.programs.spectra.integration_seconds/2
    datestr = dt_tm_mk(js2jd(0d)+1, ctime, format='0d$ n$ Y$')

    ncdf_close, host.netcdf(0).ncid
    host.netcdf(0).ncid = -1
    instance.records = [0, n_elements(tlist)-1]

    mc_menu, extra_menu, 'View',                     1, event_handler='sdi2k_math_windmap_event', /new
    mc_menu, extra_menu, 'Time Chooser',             0, event_handler='sdi2k_math_windmap_event'
    mc_menu, extra_menu, 'Toggle Background Color',  0, event_handler='sdi2k_math_windmap_event'
    mc_menu, extra_menu, 'Perspective',              1, event_handler='sdi2k_math_windmap_event'
    mc_menu, extra_menu, 'Map',                      0, event_handler='sdi2k_math_windmap_event'
    mc_menu, extra_menu, 'Sky',                      2, event_handler='sdi2k_math_windmap_event'
    mc_menu, extra_menu, 'Orientation',              1, event_handler='sdi2k_math_windmap_event'
    mc_menu, extra_menu, 'Magnetic North',           0, event_handler='sdi2k_math_windmap_event'
    mc_menu, extra_menu, 'Geographic North',         0, event_handler='sdi2k_math_windmap_event'
    mc_menu, extra_menu, 'Magnetic Noon',            2, event_handler='sdi2k_math_windmap_event'
    mc_menu, extra_menu, 'Redraw',                   2, event_handler='sdi2k_math_windmap_event'
    mnu_xwindow_autorun, instance, topname='sdi2ka_top', extra_menu=extra_menu

    sdi2k_math_windmap_plot, instance.description
end

;==========================================================================================
; This is the (required) class method for creating a new instance of the sdi2k_math_windmap object. It
; would normally be an empty procedure.  Nevertheless, it MUST be present, as the last procedure in
; the methods file, and it MUST have the same name as the methods file.  By calling this
; procedure, the caller forces all preceeding routines in the methods file to be compiled,
; and so become available for subsequent use:

pro sdi2k_math_windmap
end

