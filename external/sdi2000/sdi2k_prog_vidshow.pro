; >>>> begin comments
;==========================================================================================
;
; >>>> McObject Class: sdi2k_prog_vidshow
;
; This file contains the McObject method code for sdi2k_prog_vidshow objects:
;
; Mark Conde (Mc), Fairbanks, October 2000.
;
; >>>> end comments
; >>>> begin declarations
;         menu_name = Direct Video
;        class_name = sdi2k_prog_vidshow
;       description = SDI Program - Direct Video
;           purpose = SDI operation
;       idl_version = 5.2
;  operating_system = Windows NT4.0 terminal server 
;            author = Mark Conde
; >>>> end declarations


;==========================================================================================
; This is the (required) "new" method for this McObject:

pro sdi2k_prog_vidshow_new, instance, dynamic=dyn, creator=cmd
;---First, properties specific to this object:
    cmd = 'instance = {sdi2k_prog_vidshow, '
    cmd = cmd + 'specific_cleanup: ''sdi2k_prog_vidshow_specific_cleanup'', '    
;---Now add fields common to all SDI objects. These will be grouped as sub-structures:
    sdi2k_common_fields, cmd, automation=automation, geometry=geometry
;---Next, add the required fields, whose specifications are read from the 'declarations'
;   section of the comments at the top of this file:
    whoami, dir, file    
    obj_reqfields, dir+file, cmd, dynamic=dyn
;---Now, create the instance:
    status = execute(cmd)
end

;==========================================================================================
; This is the event handler for events generated by the sdi2k_prog_vidshow object:
pro sdi2k_prog_vidshow_event, event
    widget_control, event.top, get_uvalue=info
    wid_pool, 'Settings: ' + info.wtitle, widx, /get
    if not(widget_info(widx, /valid_id)) then return
    widget_control, widx, get_uvalue=vidshow_settings
    if widget_info(event.id, /valid_id) and vidshow_settings.automation.show_on_refresh then widget_control, event.id, /show

;---Check for a timer tick:    
    if tag_names(event, /structure_name) eq 'WIDGET_TIMER' then begin
       sdi2k_prog_vidshow_tik, info.wtitle
       if vidshow_settings.automation.timer_ticking then widget_control, widx, timer=vidshow_settings.automation.timer_interval
       return
    endif
    
;---Check for a new frame event sent by the control module:
    nm      = 0
    matched = where(tag_names(event) eq 'NAME', nm)
    if nm gt 0 then begin
       if event.(matched(0)) eq 'NewFrame' then sdi2k_prog_vidshow_tik, info.wtitle
       return
    endif

;---Get the menu name for this event:
    widget_control, event.id, get_uvalue=menu_item
    if n_elements(menu_item) eq 0 then menu_item = 'Nothing valid was selected'
end

;==========================================================================================
; This is the routine that updates the actual plot:
pro sdi2k_prog_vidshow_tik, wtitle, redraw=redraw, _extra=_extra
@sdi2kinc.pro

;---Get settings information for this instance of the output xwindow and this instance of 
;   the plot program itself:
    wid_pool, wtitle, widx, /get
    if not(widget_info(widx, /valid_id)) then return
    widget_control, widx, get_uvalue=info
    wid_pool, 'Settings: ' + wtitle, sidx, /get
    if not(widget_info(sidx, /valid_id)) then return
    widget_control, sidx, get_uvalue=vidshow_settings

    host.programs.direct_video.update_count = host.programs.direct_video.update_count + 1    
    if host.programs.direct_video.scan_etalon and $
       host.controller.scheduler.job_semaphore eq 'No scheduled job' then sdi2k_etalon_scan
    if host.programs.direct_video.update_count ge host.programs.direct_video.update_divisor then begin
       host.programs.direct_video.update_count = 0
       if !d.name ne 'Z' and !d.name ne 'PS' then wset, info.wid
       case strupcase(host.programs.direct_video.brightness_function) of
            'BYTSCL': tv, host.colors.greymin + bytscl(view, top=host.colors.greymax - host.colors.greymin - 2)
            'SQRT': tv, host.colors.greymin + bytscl(sqrt(view), top=host.colors.greymax - host.colors.greymin - 2)
       else: tv, host.colors.greymin + view < host.colors.greymax
       endcase
       if host.programs.direct_video.show_center then begin
          plots, [0, n_elements(view(*,0))], [host.operation.zones.y_center, host.operation.zones.y_center], $
                 /device, color=host.colors.cyan, linestyle=1
          plots, [host.operation.zones.x_center, host.operation.zones.x_center], [0, n_elements(view(0,*))], $
                 /device, color=host.colors.cyan, linestyle=1
       endif
       empty
    endif

    widget_control, sidx, set_uvalue=vidshow_settings
;---Check if we need to make a GIF file:
    sdi2k_plugin_gif, info
end

;==========================================================================================
;   Cleanup jobs:
pro sdi2k_prog_vidshow_specific_cleanup, widid
@sdi2kinc.pro
    if host.controller.scheduler.job_semaphore eq 'No scheduled job' then $
       sdi2k_set_shutters, camera='closed', laser='closed'
end

;==========================================================================================
; This is the (required) "autorun" method for this McObject. If no autorun action is 
; needed, then this routine should simply exit with no action:

pro sdi2k_prog_vidshow_autorun, instance
@sdi2kinc.pro
    instance.geometry.xsize = n_elements(view(*,0))
    instance.geometry.ysize = n_elements(view(0,*))
    instance.automation.timer_interval = 1.
    instance.automation.timer_ticking = 0
    if host.controller.scheduler.job_semaphore eq 'No scheduled job' then $
       sdi2k_set_shutters, camera='open', laser='closed'
    mnu_xwindow_autorun, instance, topname='sdi2k_top'    
end

;==========================================================================================
; This is the (required) class method for creating a new instance of the sdi2k_prog_vidshow object. It
; would normally be an empty procedure.  Nevertheless, it MUST be present, as the last procedure in 
; the methods file, and it MUST have the same name as the methods file.  By calling this
; procedure, the caller forces all preceeding routines in the methods file to be compiled, 
; and so become available for subsequent use:

pro sdi2k_prog_vidshow
end

