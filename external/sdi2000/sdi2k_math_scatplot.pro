; >>>> begin comments
;==========================================================================================
;
; >>>> McObject Class: sdi2k_math_scatplot
;
; This file contains the McObject method code for sdi2k_math_scatplot objects:
;
; Mark Conde Fairbanks, November 2000.
;
; >>>> end comments
; >>>> begin declarations
;         menu_name = Scatter Plotter
;        class_name = sdi2k_math_scatplot
;       description = SDI Analysis - Scatter Plot
;           purpose = SDI analysis
;       idl_version = 5.2
;  operating_system = Windows NT4.0 terminal server 
;            author = Mark Conde
; >>>> end declarations

@sdi2k_ncdf.pro

;==========================================================================================
; This is the (required) "new" method for this McObject:

pro sdi2k_math_scatplot_new, instance, dynamic=dyn, creator=cmd
;---First, properties specific to this object:
    cmd = 'instance = {sdi2k_math_scatplot, '
    cmd = cmd + 'specific_cleanup: ''sdi2k_math_scatplot_specific_cleanup'', '    
    scatplot_behavior = {scatplot_behavior, prompt_for_filename: 1, $
                           menu_configurable: 1, $
                               user_editable: [0]} 
    scatplot_scale = {scatplot_scale, auto_scale: 1, $
                                      xrange: [0., 1.], $
                                      yrange: [0., 1.], $
                           menu_configurable: 1, $
                               user_editable: [0,1,2]} 
    cmd = cmd + 'behavior: scatplot_behavior, '
    cmd = cmd + 'scale: scatplot_scale, '
    cmd = cmd + 'parameter: [12,5], '
    cmd = cmd + 'zones: "Zenith", '
;---Now add fields common to all SDI objects. These will be grouped as sub-structures:
    sdi2k_common_fields, cmd, automation=automation, geometry=geometry
;---Next, add the required fields, whose specifications are read from the 'declarations'
;   section of the comments at the top of this file:
    whoami, dir, file    
    obj_reqfields, dir+file, cmd, dynamic=dyn
;---Now, create the instance:
    status = execute(cmd)
end

;==========================================================================================
; This is the event handler for events generated by the sdi2k_math_scatplot object:
pro sdi2k_math_scatplot_event, event
    widget_control, event.top, get_uvalue=info
    wid_pool, 'Settings: ' + info.wtitle, widx, /get
    if not(widget_info(widx, /valid_id)) then return
    widget_control, widx, get_uvalue=scatplot_settings
    if widget_info(event.id, /valid_id) and scatplot_settings.automation.show_on_refresh then widget_control, event.id, /show

;---Check for a timer tick:    
    if tag_names(event, /structure_name) eq 'WIDGET_TIMER' then begin
       sdi2k_math_scatplot_tik, info.wtitle
       if scatplot_settings.automation.timer_ticking then widget_control, widx, timer=scatplot_settings.automation.timer_interval
       return
    endif
    
;---Get the menu name for this event:
    widget_control, event.id, get_uvalue=menu_item
    
    if menu_item eq 'View|X-Parameter' then sdi2k_scatplot_getpar, info.wtitle, 'X'
    if menu_item eq 'View|Y-Parameter' then sdi2k_scatplot_getpar, info.wtitle, 'Y'
    if menu_item eq 'View|Zone Usage'     then sdi2k_scatplot_zones,  info.wtitle
    sdi2k_scatplot_data, info.wtitle    
    if n_elements(menu_item) eq 0 then menu_item = 'Nothing valid was selected'
end

;==========================================================================================
; This is the routine that handles timer ticks:
pro sdi2k_math_scatplot_tik, wtitle, redraw=redraw, _extra=_extra
@sdi2kinc.pro
    sdi2k_scatplot_data, wtitle
end

pro sdi2k_scatplot_getpar, wtitle, axis
@sdi2kinc.pro
    common scatplot_resarr, resarr

    wid_pool, wtitle, widx, /get
    if not(widget_info(widx, /valid_id)) then return
    widget_control, widx, get_uvalue=info
    wid_pool, 'Settings: ' + wtitle, sidx, /get
    if not(widget_info(sidx, /valid_id)) then return
    widget_control, sidx, get_uvalue=scatplot_settings

    nz = total(host.operation.zones.sectors(0:host.operation.zones.fov_rings-1))
    for j=0, n_tags(resarr(0))-1 do begin
        if n_elements(resarr(0).(j)) eq nz then begin
           if n_elements(skies) eq 0 then skies = j else skies = [skies, j]
        endif
    endfor
    skynames = tag_names(resarr(0))
    skynames = sentence_case(skynames(skies))
    mcchoice, 'Select the ' + axis + '-Parameter:', skynames, choice
    if axis eq 'X' then scatplot_settings.parameter(0) = skies(choice.index)
    if axis eq 'Y' then scatplot_settings.parameter(1) = skies(choice.index)
    widget_control, sidx, set_uvalue=scatplot_settings
end

pro sdi2k_scatplot_zones, wtitle
@sdi2kinc.pro
    wid_pool, wtitle, widx, /get
    if not(widget_info(widx, /valid_id)) then return
    widget_control, widx, get_uvalue=info
    wid_pool, 'Settings: ' + wtitle, sidx, /get
    if not(widget_info(sidx, /valid_id)) then return
    widget_control, sidx, get_uvalue=scatplot_settings

    nz = total(host.operation.zones.sectors(0:host.operation.zones.fov_rings-1))
    zz = strarr(nz)
    for j=0,nz-1 do zz(j) = 'Zone ' + strcompress(string(j), /remove_all)
    mcchoice, 'Select zone usage:', ['All', 'Zenith', 'Median', zz], choice
    scatplot_settings.zones = choice.name
    widget_control, sidx, set_uvalue=scatplot_settings
end

pro sdi2k_scatplot_data, wtitle
@sdi2kinc.pro
    common scatplot_resarr, resarr
;---Get settings information for this instance of the output xwindow and this instance of 
;   the plot program itself:
    wid_pool, wtitle, widx, /get
    if not(widget_info(widx, /valid_id)) then return
    widget_control, widx, get_uvalue=info
    wid_pool, 'Settings: ' + wtitle, sidx, /get
    if not(widget_info(sidx, /valid_id)) then return
    widget_control, sidx, get_uvalue=scatplot_settings
    if !d.name ne 'Z' and !d.name ne 'PS' then wset, info.wid
    
    sdi2k_scatter_plotter, tlist, tcen, datestr, resarr, scatplot_settings

;---Check if we need to make a GIF file:
    ;sdi2k_plugin_gif, info, js_time=timlimz(1)
end

;==========================================================================================
;   Cleanup jobs:
pro sdi2k_math_scatplot_specific_cleanup, widid
@sdi2kinc.pro
end

;==========================================================================================
; This is the (required) "autorun" method for this McObject. If no autorun action is 
; needed, then this routine should simply exit with no action:

pro sdi2k_math_scatplot_autorun, instance
@sdi2kinc.pro
    common scatplot_resarr, resarr
    device, get_screen_size=box
    instance.geometry.xsize = 0.95*box(0)
    instance.geometry.ysize = 0.75*box(1)
    instance.automation.timer_interval = 1.
    instance.automation.timer_ticking = 0
    sdi2k_reset_spectra

    if instance.behavior.prompt_for_filename then begin
       fitfile = dialog_pickfile(file=fitfile, $
                                 filter='*.' + host.operation.header.site_code, $
                                 group=widx, title='Select a file of sky spectra: ', $
                                 path=host.operation.logging.log_directory)                                 
    endif

    sdi2k_ncopen, fitfile, ncid, 0
    sdi2k_build_fitres, ncid, resarr
    sdi2k_drift_correct, resarr, source_file=fitfile
    sdi2k_physical_units, resarr
    ncdf_close, ncid
    host.netcdf(0).ncid = -1
    
    mc_menu, extra_menu, 'View',           1, event_handler='sdi2k_math_scatplot_event', /new
    mc_menu, extra_menu, 'X-Parameter', 0, event_handler='sdi2k_math_scatplot_event'
    mc_menu, extra_menu, 'Y-Parameter', 0, event_handler='sdi2k_math_scatplot_event'
    mc_menu, extra_menu, 'Zone Usage',     0, event_handler='sdi2k_math_scatplot_event'
    mc_menu, extra_menu, 'Redraw',         2, event_handler='sdi2k_math_scatplot_event'
    mnu_xwindow_autorun, instance, topname='sdi2ka_top', extra_menu=extra_menu
    
    sdi2k_scatplot_getpar, instance.description, 'X'
    sdi2k_scatplot_getpar, instance.description, 'Y'
    sdi2k_scatplot_zones,  instance.description
    sdi2k_scatplot_data,   instance.description
 end

;==========================================================================================
; This is the (required) class method for creating a new instance of the sdi2k_math_scatplot object. It
; would normally be an empty procedure.  Nevertheless, it MUST be present, as the last procedure in 
; the methods file, and it MUST have the same name as the methods file.  By calling this
; procedure, the caller forces all preceeding routines in the methods file to be compiled, 
; and so become available for subsequent use:

pro sdi2k_math_scatplot
end

